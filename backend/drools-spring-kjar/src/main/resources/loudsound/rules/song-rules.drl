package loudsound.rules;
dialect  "mvel"

import java.util.List;
import java.util.Date;

import loudsound.model.Song;
import loudsound.events.song.SongReleasedEvent;
import loudsound.events.song.SongListeningStartedEvent;
import loudsound.events.song.SongListeningEndedEvent;
import loudsound.events.song.SongListenedEvent;
import loudsound.events.song.SongSkippedEvent;
import loudsound.events.song.SongLikedEvent;
import loudsound.events.song.SongStatusChangedEvent;
import loudsound.events.song.SongEnteredTopNEvent;

import org.slf4j.Logger;
import org.drools.core.spi.KnowledgeHelper;

global org.slf4j.Logger logger;

rule "New song is released"
    lock-on-active
    when
        $song: Song()
    then
        insert(new SongReleasedEvent(
            $song.artist,
            new Date(drools.getWorkingMemory()
                .getSessionClock()
                .getCurrentTime()),
            $song.id));
        logger.debug("Fired rule: '{}'", drools.getRule().getName());
        logger.debug("New song released: '{} - {}'", $song.artist, $song.title);
end

rule "Song is liked"
    no-loop
    when
        $song: Song($sid: id) @Watch(!likesNumber)
        $e: SongLikedEvent($song.id == songId)
    then
        logger.debug("Fired rule: '{}'", drools.getRule().getName());
        modify ($song) { like() };
        logger.debug(
                "Song: '{} - {}' was liked {} times",
                $song.artist,
                $song.title,
                $song.getLikesNumber()
        );
end

rule "Song is listened"
    no-loop
    when
        $song: Song($sid: id) @Watch(!timesListenedNumber)
        $start: SongListeningStartedEvent($sid == songId, $scauser: causerId)
        $end: SongListeningEndedEvent($sid == songId, $scauser == causerId, this after[5s] $start)
    then
        logger.debug("Fired rule: '{}'", drools.getRule().getName());
        modify ($song) { listen() };
        insert(new SongListenedEvent(
            $scauser,
            new Date(drools.getWorkingMemory()
                .getSessionClock()
                .getCurrentTime()),
            $song.id));
        delete($start);
        delete($end);
        logger.debug(
                "Song: '{} - {}' was listened {} times",
                $song.artist,
                $song.title,
                $song.getTimesListenedNumber()
        );
end

rule "Song is skipped"
    no-loop
    when
        $song: Song($sid: id) @Watch(!timesSkippedNumber)
        $start: SongListeningStartedEvent($sid == songId, $scauser: causerId)
        $end: SongListeningEndedEvent($sid == songId, $scauser == causerId, this after[0s, 5s] $start)
    then
        logger.debug("Fired rule: '{}'", drools.getRule().getName());
        modify ($song) { skip() };
        insert(new SongSkippedEvent(
            $scauser,
            new Date(drools.getWorkingMemory()
                .getSessionClock()
                .getCurrentTime()),
            $song.id));
        delete($start);
        delete($end);
        logger.debug(
                "Song: '{} - {}' was skipped {} times",
                $song.artist,
                $song.title,
                $song.getTimesSkippedNumber()
        );
end

rule "Song is declared Boring"
    when
        $song: Song(
            $sid: id,
            status == Song.Status.OK,
            timesSkippedNumber > 1,
            ((double) timesSkippedNumber / timesListenedNumber) > 2)

        // timeout
        Number(intValue == 0) from accumulate (
            $e: SongStatusChangedEvent(songId == $sid,  status == Song.Status.BORING)
            over window:time( 15m ),
            count($e)
        )
    then
        logger.debug("Fired rule: '{}'", drools.getRule().getName());
        insert(new SongStatusChangedEvent(
            new Date(drools.getWorkingMemory()
                .getSessionClock()
                .getCurrentTime()),
            $song.id,
            Song.Status.BORING ));
        modify ($song) { setStatus(Song.Status.BORING) };
        logger.debug(
                "Song: '{} - {}' was declared {}",
                $song.artist,
                $song.title,
                $song.status
        );
end

rule "Song is redeclared Ok"
    when
        $song: Song(
            $sid: id,
            status == Song.Status.BORING
        )
        // timeout
        Number(intValue == 0) from accumulate (
            $e: SongStatusChangedEvent(songId == $sid, status == Song.Status.OK)
            over window:time( 15m ),
            count($e)
        )
        $listenedNum: Number() from accumulate(
            $e1: SongListenedEvent(songId == $sid)
            over window:time( 48h ),
            count($e1)
        )
        $skippedNum: Number($listenedNum > 2 * intValue) from accumulate(
            $e2: SongSkippedEvent(songId == $sid)
            over window:time( 48h ),
            count($e2)
        )
    then
        logger.debug("Fired rule: '{}'", drools.getRule().getName());
        insert(new SongStatusChangedEvent(
            new Date(drools.getWorkingMemory()
                .getSessionClock()
                .getCurrentTime()),
            $song.id,
            Song.Status.OK ));
        modify ($song) { setStatus(Song.Status.OK) };
        logger.debug(
                "Song: '{} - {}' was declared {}",
                $song.artist,
                $song.title,
                $song.status
        );
end

rule "Song entered top N chart (when there are fewer songs than N)"
    when
        Number(intValue <= 3) from accumulate (
            $e: SongReleasedEvent(),
            count($e)
        )
        $currentTopNSongIds: List() from accumulate(
            SongEnteredTopNEvent($topNSongId: songId, !revoked),
            collectList($topNSongId)
        )
        $song: Song(id not memberOf $currentTopNSongIds)
    then
        logger.debug("Fired rule: '{}'", drools.getRule().getName());
        insert(new SongEnteredTopNEvent(
        new Date(drools.getWorkingMemory()
            .getSessionClock()
            .getCurrentTime()),
        $song.id));
        logger.debug(
                "Song: '{} - {}' reached topN",
                $song.artist,
                $song.title
        );
end

rule "Song entered top N chart (when there are more songs than N)"
    when
        $currentTopNSongIds: List() from accumulate(
            SongEnteredTopNEvent($topNSongId: songId, !revoked),
            collectList($topNSongId)
        )
        $lastSongInCurrentTopN: Song() from accumulate(
            $currentTopNSong: Song(id memberOf $currentTopNSongIds, $currentListens: timesListenedNumber),
            init ( Song min = null; ),
            action (
                if (min == null || min.timesListenedNumber > $currentListens) {
                    min = $currentTopNSong;
                }
            ),
            result( min )
        )
        $topSongInCandidates: Song(timesListenedNumber > $lastSongInCurrentTopN.timesListenedNumber) from accumulate(
            $currentCandidatesNSong: Song(id not memberOf $currentTopNSongIds, status == Song.Status.OK, $currentListens: timesListenedNumber),
            init ( Song max = null; ),
            action (
                if (max == null || max.timesListenedNumber < $currentListens) {
                  max = $currentCandidatesNSong;
                }
            ),
            result( max )
        )
        $lastSongInCurrentTopNEvent: SongEnteredTopNEvent(songId == $lastSongInCurrentTopN.id, !revoked)
    then
        logger.debug("Fired rule: '{}'", drools.getRule().getName());

        insert(new SongEnteredTopNEvent(
            new Date(drools.getWorkingMemory()
                .getSessionClock()
                .getCurrentTime()),
            $topSongInCandidates.id));
        logger.debug(
                "Song: '{} - {}' reached topN",
                $topSongInCandidates.artist,
                $topSongInCandidates.title
        );

        modify ($lastSongInCurrentTopNEvent) { revoke() };
        logger.debug(
                "Song: '{} - {}' was revoked from topN",
                $lastSongInCurrentTopN.artist,
                $lastSongInCurrentTopN.title
        );
end

rule "Song is declared Popular"
    when
        $song: Song(
            $sid: id,
            status == Song.Status.OK
        )
        // timeout
        Number(intValue == 0) from accumulate (
            $e: SongStatusChangedEvent(songId == $sid, status == Song.Status.POPULAR)
            over window:time( 15m ),
            count($e)
        )
        Number(intValue == 1) from accumulate(
            $e1: SongEnteredTopNEvent(songId == $sid, !revoked)
            over window:time( 24h ),
            count($e1)
        )
        Number(intValue > 10) from accumulate(
            $e2: SongLikedEvent(songId == $sid)
            over window:time( 24h ),
            count($e2)
        )
    then
        logger.debug("Fired rule: '{}'", drools.getRule().getName());
        insert(new SongStatusChangedEvent(
            new Date(drools.getWorkingMemory()
                .getSessionClock()
                .getCurrentTime()),
            $song.id,
            Song.Status.POPULAR ));
        modify ($song) { setStatus(Song.Status.POPULAR) };
        logger.debug(
                "Song: '{} - {}' was declared {}}",
                $song.artist,
                $song.title,
                $song.status
        );
end

rule "Song is not Popular anymore"
    when
        $song: Song(
            $sid: id,
            status == Song.Status.POPULAR
        )

        // timeout
        Number(intValue == 0) from accumulate (
            $e: SongStatusChangedEvent(songId == $sid, status == Song.Status.OK)
            over window:time( 15m ),
            count($e)
        )

        Number(intValue == 0) from accumulate(
            $e: SongEnteredTopNEvent(songId == $sid, !revoked)
            over window:time( 24h ),
            count($e)
        )
    then
        logger.debug("Fired rule: '{}'", drools.getRule().getName());
        insert(new SongStatusChangedEvent(
            new Date(drools.getWorkingMemory()
                .getSessionClock()
                .getCurrentTime()),
            $song.id,
            Song.Status.OK ));
        modify ($song) { setStatus(Song.Status.OK) };
        logger.debug(
                "Song: '{} - {}' was declared {}}",
                $song.artist,
                $song.title,
                $song.status
        );
end
